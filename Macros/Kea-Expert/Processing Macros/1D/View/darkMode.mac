###########################################################
# Change the colors in current or all plots to 'dark mode'
#
# Author: C Eccles
# Copyright Magritek 2022-24
#
# https://www.heavy.ai/blog/12-color-palettes-for-telling-better-stories-with-your-data
#
###########################################################

procedure(darkMode)

# Allow editing
   if(EditIfShiftPressed(getmacropath(),getmacroname()))
      return
   endif

# For undo
   func1d("copy plot")

   pp = curplot("1d")->parent
   (w,h) = pp->size()
   for(x = 1 to w)
      for(y = 1 to h)
         pt = pp->subplot(x,y)
         try
            pt->draw("false")
            cd("$appdir$\\Macros\\Kea-Expert\\Processing Macros\\1DPlotAnalyser\\Dark Mode")
            plotPreferences() 
            tracePreferences() 
            (realCol,imagCol) = :getColors(ptNew)
            LoadExpertPlotPreferences:applyPref(pt,realCol,imagCol)
            :changeTextColors(pt)
            pt->draw("true")
         catch
            pt->draw("true")
         endtry
      next(y)
   next(x)

endproc()

procedure(allplots)

   bak = getcwd()
   cd("$appdir$\\Macros\\Kea-Expert\\Processing Macros\\1DPlotAnalyser\\Dark Mode")
   plt = curplot("1d")
   for(k = 1 to gView->nr1DPlots)
      ptNew = eval("gView->g$k$->subplot(1,1)")
      if(ptNew->parent->visible() == "true")
         (realCol,imagCol) = :getColors(ptNew)
         curplot(ptNew)
         plotPreferences() 
         tracePreferences()
         :changeTextColors(ptNew)
         LoadExpertPlotPreferences:applyPref(ptNew,realCol,imagCol)
      endif
   next(k)
   curplot(plt)
   cd(bak)

endproc()

# Change black text to white 
procedure(changeTextColors, plt)

   textArray  = plt->gettext
   sa = structarray(1)
   if(textArray != null)
      for(k = 0 to size(textArray)-1)
         txtObj = textArray[k]
         col = txtObj->color
         if(col == [0,0,0])
            plt->rmtext(txtObj->text)
            txtObj->color = [255,255,255]
            sa[0] = txtObj
            plt->addtext(sa)
         endif
      next(k)
   endif

endproc()


procedure(getColors, plt)

   if(plt->axes->type == "box_y_independent") # Stacked plot

      trcLst = plt->tracelist
      sz = size(trcLst)
      realCol = matrix(3,sz)
      imagCol = matrix(3,sz)
      for(k = 0 to sz-1)
         r = k*255/(sz-1)
         g = 255-r 
         realCol[~,k] = [r,g,0]
         imagCol[~,k] = [r,g,0]/2
      next(k)

   else # Normal plot

      realCol = [0xE6,0x00,0x49;
                 0x0B,0xB4,0xFF;
                 0x50,0xE9,0x91;
                 0xe6,0xD8,0x00;
                 0x9B,0x19,0xF5;
                 0xFF,0xA3,0x00;
                 0xDC,0x0A,0xB4;
                 0xB3,0xD4,0xFF;
                 0x00,0xBF,0xA0]
   
      imagCol = realCol*0.6

   endif
   

endproc(realCol, imagCol)
