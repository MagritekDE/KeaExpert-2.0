#####################################################
# Procedures to load native Tecmag data
# Required procedures:
# 
# types:         returns list of supported data types
# isdata:        returns 1 if Tecmag data 0 otherwise
# getname:       returns name of data file
# getparameters: returns data parameter list
# getdata:       returns data
#
######################################################

#############################################
# Return a list of file types supported by 
# Tecmag
#############################################

procedure(types)

  lst = ["Tecmag"]

endproc(lst)

#############################################
# Return 1 is the type if data in the current
# directory is Tecmag, 0 otherwise
#############################################

procedure(isdata)

  try
     lst = getfilelist(".")
  catch
     return(0)
  endtry
  sz = size(lst)
  for(k = 0 to sz-1)
     ext = getext(lst[k])
     if(ext == "tnt")
        return(1)
     endif
  next(k)


endproc(0)

#############################################
# Tecmag data can load without exp.par file
#############################################

procedure(needexppar)


endproc("no")

#############################################
# Returns name of Tecmag data file
# blank otherwise
#############################################

procedure(getname)

  lst = getfilelist(".")
  sz = size(lst)
  for(k = 0 to sz-1)
     ext = getext(lst[k])
     if(ext == "tnt")
        return(lst[k])
     endif
  next(k)

endproc("")

#############################################
# Return Tecmag file parameters
#############################################

procedure(getparameters,dataFile)


# Read binary header
   import1dpar("ab", "binary", "xyrc", "real",
               "fls", "long", "machine", "littleend",
               "fileheader",0)
   y = import1d(dataFile,1056)

   header = 1056

   off = 5

   1dim = y[0+off]
   2dim = y[1+off]
   3dim = y[2+off]
   4dim = y[3+off]


   filetype = "Tecmag"
   dig_shift = 0

   p = ["1dim = $1dim$",
        "2dim = $2dim$",
        "3dim = $3dim$",
        "4dim = $4dim$",
        "1dzf = $1dim$",
        "2dzf = $2dim$",
        "3dzf = $3dim$",
        "fileType = \"$filetype$\"",
        "matrixIn = \"\"",
        "1dfltr = \"none\"",
        "2dfltr = \"none\"",
        "3dfltr = \"none\"",
        "1dft = \"FTEcho\"",
        "2dft = \"FTEcho\"",
        "3dft = \"FTEcho\""
        "digShift = $dig_shift$",
        "nrType = \"complex\"",
        "header = $header$"]

endproc(p)


#############################################
# Load Tecmag data and store to global
# variable curmatrix and curmatrix.1d/2d/3d
#############################################

procedure(getdata,dataFile,pl)

   assignlist(pl)

# Read 1D data 
# Data is organised as [Real (float), Imag (float)]
   if(1dim > 1 & 2dim == 1 & 3dim == 1)
      import1dpar("ab", "binary", "xyrc", "complex",
                  "fls", "float", "machine", "littleend",
                  "fileheader",header)
      mat = import1d(dataFile,1dim)
     
# Read 2D data 
   elseif(1dim > 1 & 2dim > 1 & 3dim == 1)
      import2dpar("ab", "binary", "xyrc", "complex",
                  "fls", "float", "machine", "littleend",
                  "fileheader",header)
      mat = import2d(dataFile,1dim,2dim)

# Read 3D data 
   elseif(1dim > 1 & 2dim > 1 & 3dim > 1)
      import3dpar("ab", "binary", "xyrc", "complex",
                  "fls", "float", "machine", "littleend",
                  "fileheader",header)
      mat = import3d(dataFile,1dim,2dim,3dim)
   endif

   assign(matrixIn,mat,"global")
   assign("curmatrix",matrixIn,"global")


endproc()


